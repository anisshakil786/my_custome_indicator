// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Muhammad_Anis

//@version=6
indicator("All in One", "AIO", true, max_lines_count = 500, max_boxes_count = 1000)

get_line_type(_style) =>
    switch _style
        'Solid' => line.style_solid
        'Dotted' => line.style_dotted
        'Dashed' => line.style_dashed

get_size(x) =>
    switch x
        'Auto' => size.auto
        'Tiny' => size.tiny
        'Small' => size.small
        'Normal' => size.normal
        'Large' => size.large
        'Huge' => size.huge

type kz
    string _title
    box[] _box
    line[] _hi_line
    line[] _md_line
    line[] _lo_line
    bool[] _hi_valid
    bool[] _md_valid
    bool[] _lo_valid

type hz
    line[] LN
    label[] LB
    bool[] CO

type dwm_hl
    line[] hi_line
    line[] lo_line
    label[] hi_label
    label[] lo_label
    bool hit_high = false
    bool hit_low = false

type dwm_info
    string tf
    float o = na
    float h = na
    float l = na
    float ph = na
    float pl = na

var hz_h1 = hz.new(array.new_line(), array.new_label(), array.new_bool())
var hz_h2 = hz.new(array.new_line(), array.new_label(), array.new_bool())

var d_hl = dwm_hl.new(array.new_line(), array.new_line(), array.new_label(), array.new_label())
var w_hl = dwm_hl.new(array.new_line(), array.new_line(), array.new_label(), array.new_label())
var m_hl = dwm_hl.new(array.new_line(), array.new_line(), array.new_label(), array.new_label())

var d_info = dwm_info.new("D")
var w_info = dwm_info.new("W")
var m_info = dwm_info.new("M")

var d_sep_line = array.new_line()
var w_sep_line = array.new_line()
var m_sep_line = array.new_line()

var d_line = array.new_line()
var w_line = array.new_line()
var m_line = array.new_line()

var d_label = array.new_label()
var w_label = array.new_label()
var m_label = array.new_label()

var float ma1 = na
var float ma2 = na
var float ma3 = na
var float ma4 = na

string g_settings              = "Settings  ━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━"
string g_snkz                  = "Sessions and Killzones  ━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━"
string g_killzones             = "Killzones  ━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━"
string g_dwm                   = "Previous Period H/L  ━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━"
string g_ma                    = "MA  ━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━"
string g_extraoptions          = "Extra Options  ━━━━━━━━━━━━━━━━━━━━━━━━━━━━━━"

enable_sessionsandkillzones = input.bool(true, "Enable Sessions and Killzones")
enable_dwm                  = input.bool(true, "Enable Day - Week - Month")
enable_ma                   = input.bool(true, "Enable MA")
enable_extra_options        = input.bool(true, "Enable Extra Options")

max_days                    = input.int(1, "Session Drawing Limit", minval=1, maxval=30, tooltip = "Only this many drawings will be kept on the chart, for each selected drawing type (killzone boxes, pivot lines, open lines, etc.)", group = g_settings)
tf_limit                    = input.timeframe("30", "Timeframe Limit", tooltip = "Drawings will not appear on timeframes greater than or equal to this", group = g_settings)
gmt_tz                      = input.string('America/New_York', "Timezone", options = ['America/New_York', 'GMT-12', 'GMT-11', 'GMT-10', 'GMT-9', 'GMT-8', 'GMT-7', 'GMT-6', 'GMT-5', 'GMT-4', 'GMT-3', 'GMT-2', 'GMT-1', 'GMT+0', 'GMT+1', 'GMT+2', 'GMT+3', 'GMT+4', 'GMT+5', 'GMT+6', 'GMT+7', 'GMT+8', 'GMT+9', 'GMT+10', 'GMT+11', 'GMT+12', 'GMT+13', 'GMT+14'], tooltip = "Note GMT is not adjusted to reflect Daylight Saving Time changes", group = g_settings)
lbl_size                    = get_size(input.string('Small', "Label Size", options = ['Auto', 'Tiny', 'Small', 'Normal', 'Large', 'Huge'], tooltip = "The size of all labels", group = g_settings))
txt_color                   = input.color(color.black, "Text Color", tooltip = "The color of all label and table text", group = g_settings)

use_asian_session           = input.bool(true, "Asian Session", inline = "ASIAN_SESSION", group = g_snkz)
as_txt                      = ""
asian_session               = input.session("1800-0400", "", inline = "ASIAN_SESSION", group = g_snkz)
as_color                    = input.color(#00bcd4, "", inline = "ASIAN_SESSION", group = g_snkz)

use_london_session          = input.bool(true, "London Session", inline = "LONDON_SESSION", group = g_snkz)
ls_txt                      = ""
london_session              = input.session("0300-1200", "", inline = "LONDON_SESSION", group = g_snkz)
ls_color                    = input.color(#2962ff, "", inline = "LONDON_SESSION", group = g_snkz)

use_newyork_session         = input.bool(true, "NewYork Session", inline = "NEWYORK_SESSION", group = g_snkz)
nys_txt                     = ""
newyork_session             = input.session("0800-1600", "", inline = "NEWYORK_SESSION", group = g_snkz)
nys_color                   = input.color(#089981, "", inline = "NEWYORK_SESSION", group = g_snkz)

use_asia_killzone           = input.bool(true, "", inline = "ASIA", group = g_snkz)
ak_txt                      = input.string("Asia", "", inline = "ASIA", group = g_snkz)
asia                        = input.session("2000-0000", "", inline = "ASIA", group = g_snkz)
ak_color                    = input.color(#000000, "", inline = "ASIA", group = g_snkz)

use_london_killzone         = input.bool(true, "", inline = "LONDON", group = g_snkz)
lo_txt                      = input.string("London", "", inline = "LONDON", group = g_snkz)
london                      = input.session("0200-0500", "", inline = "LONDON", group = g_snkz)
lo_color                    = input.color(#000000, "", inline = "LONDON", group = g_snkz)

use_nyam_killzone           = input.bool(true, "", inline = "NYAM", group = g_snkz)
na_txt                      = input.string("NY AM", "", inline = "NYAM", group = g_snkz)
nyam                        = input.session("0930-1100", "", inline = "NYAM", group = g_snkz)
na_color                    = input.color(#000000, "", inline = "NYAM", group = g_snkz)

use_nylu                    = input.bool(true, "", inline = "NYLU", group = g_snkz)
nl_txt                      = input.string("NY Lunch", "", inline = "NYLU", group = g_snkz)
nylu                        = input.session("1200-1300", "", inline = "NYLU", group = g_snkz)
nl_color                    = input.color(#f57f17, "", inline = "NYLU", group = g_snkz)

use_nypm_killzone           = input.bool(true, "", inline = "NYPM", group = g_snkz)
np_txt                      = input.string("NY PM", "", inline = "NYPM", group = g_snkz)
nypm                        = input.session("1330-1600", "", inline = "NYPM", group = g_snkz)
np_color                    = input.color(#000000, "", inline = "NYPM", group = g_snkz)

show_kz                     = input.bool(true, "Show Killzone Boxes", inline = "KZ", group = g_snkz)
show_kz_text                = input.bool(true, "Display Text", inline = "KZ", group = g_snkz)
show_pivots                 = input.bool(true, "Show Pivots", inline = "PV", group = g_snkz)
show_midpoints              = input.bool(true, "Show Pivots Midpoints", inline = "PV", group = g_snkz)
stop_midpoints              = input.bool(true, "Stop Once Mitigated", inline = "mp", group = g_snkz)
use_alerts                  = input.bool(true, "Alert Broken Pivots", inline = "mp", tooltip = "The desired killzones must be enabled at the time that an alert is created, along with the show pivots option, in order for alerts to work", group = g_snkz)
ext_pivots                  = input.string("Until Mitigated", "Extend Pivots...", options = ['Until Mitigated', 'Past Mitigation'], group = g_snkz)
ext_which                   = input.string("Most Recent", "...From Which Sessions", options = ['Most Recent', 'All'], group = g_snkz)

box_transparency            = input.int(92, "Box Transparency", 0, 100, group = g_snkz)
text_transparency           = input.int(94, "Text Transparency", 0, 100, group = g_snkz)
kzp_style                   = get_line_type(input.string(defval = 'Solid', title = "Pivot Style", options = ['Solid', 'Dotted', 'Dashed'], inline = "KZP", group = g_snkz))
kzp_width                   = input.int(1, "", inline = "KZP", group = g_snkz)
kzm_style                   = get_line_type(input.string(defval = 'Dotted', title = "Midpoint Style", options = ['Solid', 'Dotted', 'Dashed'], inline = "KZM", group = g_snkz))
kzm_width                   = input.int(1, "", inline = "KZM", group = g_snkz)

alert_HL                    = input.bool(true, "Alert High/Low Break", tooltip = "Alert when any selected highs and lows are traded through. The desired timeframe's high/low option must be enabled at the time that an alert is created", group = g_dwm)

show_d_open                 = input.bool(false, "D Open", inline = "DO", group = g_dwm)
dhl                         = input.bool(true, "High/Low", inline = "DO", tooltip = "", group = g_dwm)
ds                          = input.bool(false, "Separators", inline = "DO", tooltip = "Mark where a new day begins", group = g_dwm)
tf_limit_daily              = input.timeframe("240", "", inline = "DO", tooltip = "Drawings will not appear on timeframes greater than or equal to this", group = g_dwm)
d_color                     = input.color(color.red, "", inline = "DO", group = g_dwm)

show_w_open                 = input.bool(false, "W Open", inline = "WO", group = g_dwm)
whl                         = input.bool(true, "High/Low", inline = "WO", tooltip = "", group = g_dwm)
ws                          = input.bool(false, "Separators", inline = "WO", tooltip = "Mark where a new week begins", group = g_dwm)
tf_limit_weekly             = input.timeframe("D", "", inline = "WO", tooltip = "Drawings will not appear on timeframes greater than or equal to this", group = g_dwm)
w_color                     = input.color(color.red, "", inline = "WO", group = g_dwm)

show_m_open                 = input.bool(false, "M Open", inline = "MO", group = g_dwm)
mhl                         = input.bool(true, "High/Low", inline = "MO", tooltip = "", group = g_dwm)
ms                          = input.bool(false, "Separators", inline = "MO", tooltip = "Mark where a new month begins", group = g_dwm)
tf_limit_monthly            = input.timeframe("W", "", inline = "MO", tooltip = "Drawings will not appear on timeframes greater than or equal to this", group = g_dwm)
m_color                     = input.color(color.red, "", inline = "MO", group = g_dwm)

htf_style                   = get_line_type(input.string(defval = 'Solid', title = "Style", options = ['Solid', 'Dotted', 'Dashed'], inline = "D0", group = g_dwm))
htf_width                   = input.int(1, "", inline = "D0", group = g_dwm)

use_ma1                     = input.bool(true, "MA 1", inline="MA1", group = g_ma)
type_ma1                    = input.string("EMA", "", options=["SMA", "EMA", "SMMA (RMA)", "WMA", "VWMA"], inline="MA1", group = g_ma)
source_ma1                  = input.string("close", "", options=["open", "high", "low", "close", "hl2", "hlc3", "ohlc4", "hlcc4"], inline="MA1", group = g_ma)
length_ma1                  = input.int(5, "", inline="MA1", group = g_ma)
color_ma1                   = input.color(color.blue, "", inline="MA1", group = g_ma)

use_ma2                     = input.bool(true, "MA 2", inline="MA2", group = g_ma)
type_ma2                    = input.string("EMA", "", options=["SMA", "EMA", "SMMA (RMA)", "WMA", "VWMA"], inline="MA2", group = g_ma)
source_ma2                  = input.string("close", "", options=["open", "high", "low", "close", "hl2", "hlc3", "ohlc4", "hlcc4"], inline="MA2", group = g_ma)
length_ma2                  = input.int(50, "", inline="MA2", group = g_ma)
color_ma2                   = input.color(color.black, "", inline="MA2", group = g_ma)

use_ma3                     = input.bool(false, "MA 3", inline="MA3", group = g_ma)
type_ma3                    = input.string("EMA", "", options=["SMA", "EMA", "SMMA (RMA)", "WMA", "VWMA"], inline="MA3", group = g_ma)
source_ma3                  = input.string("close", "", options=["open", "high", "low", "close", "hl2", "hlc3", "ohlc4", "hlcc4"], inline="MA3", group = g_ma)
length_ma3                  = input.int(100, "", inline="MA3", group = g_ma)
color_ma3                   = input.color(color.green, "", inline="MA3", group = g_ma)

use_ma4                     = input.bool(true, "MA 4", inline="MA4", group = g_ma)
type_ma4                    = input.string("EMA", "", options=["SMA", "EMA", "SMMA (RMA)", "WMA", "VWMA"], inline="MA4", group = g_ma)
source_ma4                  = input.string("close", "", options=["open", "high", "low", "close", "hl2", "hlc3", "ohlc4", "hlcc4"], inline="MA4", group = g_ma)
length_ma4                  = input.int(200, "", inline="MA4", group = g_ma)
color_ma4                   = input.color(color.red, "", inline="MA4", group = g_ma)

use_cutoff                  = input.bool(true, "Drawing Cutoff Time", inline = "EXOCO", tooltip = "When enabled, all pivots and open price lines will stop extending at this time", group = g_extraoptions)
cutoff                      = input.session("0500-0501", "", inline = "EXOCO", group = g_extraoptions)

use_h1                      = input.bool(true, "", inline = "H1", group = g_extraoptions)
h1_text                     = input.string("TDO", "", inline = "H1", group = g_extraoptions)
h1                          = input.session("0000-0001", "", inline = "H1", group = g_extraoptions)
h1_color                    = input.color(color.black, "", inline = "H1", group = g_extraoptions)

use_h2                      = input.bool(false, "", inline = "H2", group = g_extraoptions)
h2_text                     = input.string("", "", inline = "H2", group = g_extraoptions)
h2                          = input.session("0600-0601", "", inline = "H2", group = g_extraoptions)
h2_color                    = input.color(color.black, "", inline = "H2", group = g_extraoptions)

hz_style                    = get_line_type(input.string(defval = 'Dotted', title = "Horizontal Line Style", options = ['Solid', 'Dotted', 'Dashed'], inline = "H0", group = g_extraoptions))
hz_width                    = input.int(1, "", inline = "H0", group = g_extraoptions)

var as_s = kz.new(as_txt, array.new_box(), array.new_line(), array.new_line(), array.new_line(), array.new_bool(), array.new_bool(), array.new_bool())
var ls_s = kz.new(ls_txt, array.new_box(), array.new_line(), array.new_line(), array.new_line(), array.new_bool(), array.new_bool(), array.new_bool())
var nys_s = kz.new(nys_txt, array.new_box(), array.new_line(), array.new_line(), array.new_line(), array.new_bool(), array.new_bool(), array.new_bool())

var as_kz = kz.new(ak_txt, array.new_box(), array.new_line(), array.new_line(), array.new_line(), array.new_bool(), array.new_bool(), array.new_bool())
var lo_kz = kz.new(lo_txt, array.new_box(), array.new_line(), array.new_line(), array.new_line(), array.new_bool(), array.new_bool(), array.new_bool())
var na_kz = kz.new(na_txt, array.new_box(), array.new_line(), array.new_line(), array.new_line(), array.new_bool(), array.new_bool(), array.new_bool())
var nl_kz = kz.new(nl_txt, array.new_box(), array.new_line(), array.new_line(), array.new_line(), array.new_bool(), array.new_bool(), array.new_bool())
var np_kz = kz.new(np_txt, array.new_box(), array.new_line(), array.new_line(), array.new_line(), array.new_bool(), array.new_bool(), array.new_bool())

t_as = not na(time("", asian_session, gmt_tz))
t_ls = not na(time("", london_session, gmt_tz))
t_ns = not na(time("", newyork_session, gmt_tz))
t_ak = not na(time("", asia, gmt_tz))
t_lo = not na(time("", london, gmt_tz))
t_na = not na(time("", nyam, gmt_tz))
t_nl = not na(time("", nylu, gmt_tz))
t_np = not na(time("", nypm, gmt_tz))
t_co = not na(time("", cutoff, gmt_tz))

t_h1 = not na(time("", h1, gmt_tz))
t_h2 = not na(time("", h2, gmt_tz))

var ext_current = ext_which == 'Most Recent'
var ext_past = ext_pivots == 'Past Mitigation'
var transparent = #ffffff00

get_box_color(color c) =>
    color.new(c, box_transparency)

get_text_color(color c) =>
    color.new(c, text_transparency)

get_source(source) =>
    switch source
        "open" => open
        "high" => high
        "low" => low
        "close" => close
        "hl2" => hl2
        "hlc3" => hlc3
        "ohlc4" => ohlc4
        "hlcc4" => hlcc4

update_dwm_info(dwm_info n) =>
    if timeframe.change(n.tf)
        n.ph := n.h
        n.pl := n.l
        n.o := open
        n.h := high
        n.l := low
    else
        n.h := math.max(high, n.h)
        n.l := math.min(low, n.l)

get_ma(type, source, length) =>
    type == 'SMA' ? ta.sma(source, length) : type == 'EMA' ? ta.ema(source, length) : type == 'SMMA (RMA)' ? ta.rma(source, length) : type == 'WMA' ? ta.wma(source, length) : ta.vwma(source, length)

if dhl or show_d_open
    update_dwm_info(d_info)
if whl or show_w_open
    update_dwm_info(w_info)
if mhl or show_m_open
    update_dwm_info(m_info)

dwm_sep(string tf, bool use, line[] arr, color col) =>
    if use and timeframe.change(tf)
        arr.unshift(line.new(bar_index, high * 1.0001, bar_index, low, style = htf_style, width = htf_width, extend = extend.both, color = col))
        if arr.size() > max_days
            arr.pop().delete()

dwm_open(string tf, bool use, line[] lns, label[] lbls, dwm_info n, color col, string tf_limit) =>
    if use and timeframe.in_seconds("") <= timeframe.in_seconds(tf_limit)
        if lns.size() > 0
            lns.get(0).set_x2(time)
            lbls.get(0).set_x(time)
        if timeframe.change(tf)
            lns.unshift(line.new(time, n.o, time, n.o, xloc = xloc.bar_time, style = htf_style, width = htf_width, color = col))
            lbls.unshift(label.new(time, n.o, tf + " OPEN", xloc = xloc.bar_time, style = label.style_label_left, color = transparent, textcolor = txt_color, size = lbl_size))
            if lns.size() > max_days
                lns.pop().delete()
                lbls.pop().delete()

dwm_hl(string tf, bool use, dwm_hl hl, dwm_info n, color col, string tf_limit) =>
    if use and timeframe.in_seconds("") <= timeframe.in_seconds(tf_limit)
        if hl.hi_line.size() > 0
            hl.hi_line.get(0).set_x2(time)
            hl.lo_line.get(0).set_x2(time)
            hl.hi_label.get(0).set_x(time)
            hl.lo_label.get(0).set_x(time)
        if timeframe.change(tf)
            if hl.hi_line.size() > 0
                hl.hi_line.get(0).delete()
                hl.lo_line.get(0).delete()
                hl.hi_label.get(0).delete()
                hl.lo_label.get(0).delete()
            hl.hi_line.unshift(line.new(time, n.ph, time, n.ph, xloc = xloc.bar_time, style = htf_style, width = htf_width, color = col))
            hl.lo_line.unshift(line.new(time, n.pl, time, n.pl, xloc = xloc.bar_time, style = htf_style, width = htf_width, color = col))
            hl.hi_label.unshift(label.new(time, n.ph, "P" + tf + "H", xloc = xloc.bar_time, style = label.style_label_left, color = transparent, textcolor = txt_color, size = lbl_size))
            hl.lo_label.unshift(label.new(time, n.pl, "P" + tf + "L", xloc = xloc.bar_time, style = label.style_label_left, color = transparent, textcolor = txt_color, size = lbl_size))
            hl.hit_high := false
            hl.hit_low := false
            if hl.hi_line.size() > max_days
                hl.hi_line.pop().delete()
                hl.lo_line.pop().delete()
                hl.hi_label.pop().delete()
                hl.lo_label.pop().delete()
        if hl.hi_line.size() > 0 and alert_HL
            if not hl.hit_high and high > hl.hi_line.get(0).get_y1()
                hl.hit_high := true
                alert(str.format("Hit P{0}H", tf))
            if not hl.hit_low and low < hl.lo_line.get(0).get_y1()
                hl.hit_low := true
                alert(str.format("Hit P{0}L", tf))

dwm() =>
    // DWM - Open Lines
    dwm_open("D", show_d_open, d_line, d_label, d_info, d_color, tf_limit_daily)
    dwm_open("W", show_w_open, w_line, w_label, w_info, w_color, tf_limit_weekly)
    dwm_open("M", show_m_open, m_line, m_label, m_info, m_color, tf_limit_monthly)

    // DWM - Highs and Lows
    dwm_hl("D", dhl, d_hl, d_info, d_color, tf_limit_daily)
    dwm_hl("W", whl, w_hl, w_info, w_color, tf_limit_weekly)
    dwm_hl("M", mhl, m_hl, m_info, m_color, tf_limit_monthly)

hz_line(bool use, bool t, hz hz, string txt, color col) =>
    if use and timeframe.in_seconds("") <= timeframe.in_seconds(tf_limit)
        if t and not t[1]
            hz.LN.unshift(line.new(bar_index, open, bar_index, open, style = hz_style, width = hz_width, color = col))
            hz.LB.unshift(label.new(bar_index, open, txt, style = label.style_label_left, color = transparent, textcolor = txt_color, size = lbl_size))
            array.unshift(hz.CO, false)
            if hz.LN.size() > max_days
                hz.LN.pop().delete()
                hz.LB.pop().delete()
                hz.CO.pop()
        if not t and hz.CO.size() > 0
            if not hz.CO.get(0)
                hz.LN.get(0).set_x2(bar_index)
                hz.LB.get(0).set_x(bar_index)
                if (use_cutoff ? t_co : false)
                    hz.CO.set(0, true)

del_kz(kz k) =>
    if k._box.size() > max_days
        k._box.pop().delete()
    if k._hi_line.size() > max_days
        k._hi_line.pop().delete()
        k._lo_line.pop().delete()
        k._hi_valid.pop()
        k._lo_valid.pop()
        if show_midpoints
            k._md_line.pop().delete()
            k._md_valid.pop()

adjust_in_kz(kz kz, bool t) =>
    if t
        kz._box.get(0).set_right(time)
        kz._box.get(0).set_top(math.max(kz._box.get(0).get_top(), high))
        kz._box.get(0).set_bottom(math.min(kz._box.get(0).get_bottom(), low))

        if show_pivots and kz._hi_line.size() > 0
            kz._hi_line.get(0).set_x2(time)
            if high > kz._hi_line.get(0).get_y1()
                kz._hi_line.get(0).set_xy1(time, high)
                kz._hi_line.get(0).set_xy2(time, high)

            kz._lo_line.get(0).set_x2(time)
            if low < kz._lo_line.get(0).get_y1()
                kz._lo_line.get(0).set_xy1(time, low)
                kz._lo_line.get(0).set_xy2(time, low)

            if show_midpoints
                kz._md_line.get(0).set_x2(time)
                kz._md_line.get(0).set_xy1(time, math.avg(kz._hi_line.get(0).get_y2(), kz._lo_line.get(0).get_y2()))
                kz._md_line.get(0).set_xy2(time, math.avg(kz._hi_line.get(0).get_y2(), kz._lo_line.get(0).get_y2()))

adjust_out_kz(kz kz, bool t) =>
    if kz._box.size() > 0 and show_pivots
        for i = 0 to kz._box.size() - 1
            if not ext_current or (ext_current and i == 0)
                if ext_past or kz._hi_valid.get(i)
                    kz._hi_line.get(i).set_x2(time)
                if high > kz._hi_line.get(i).get_y1() and kz._hi_valid.get(i)
                    if use_alerts and i == 0
                        alert("Broke " + kz._title + " High", alert.freq_once_per_bar)
                    kz._hi_valid.set(i, false)
//                else if use_cutoff and t_co
//                    kz._hi_valid.set(i, false)

                if ext_past or kz._lo_valid.get(i)
                    kz._lo_line.get(i).set_x2(time)
                if low < kz._lo_line.get(i).get_y1() and kz._lo_valid.get(i)
                    if use_alerts and i == 0
                        alert("Broke " + kz._title + " Low", alert.freq_once_per_bar)
                    kz._lo_valid.set(i, false)
//                else if use_cutoff and t_co
//                    kz._lo_valid.set(i, false)

                if show_midpoints and not t
                    if stop_midpoints ? kz._md_valid.get(i) : true
                        kz._md_line.get(i).set_x2(time)
                        if kz._md_valid.get(i) and low <= kz._md_line.get(i).get_y1() and high >= kz._md_line.get(i).get_y1()
                            kz._md_valid.set(i, false)
            else
                break

manage_kz(kz kz, bool use, bool t, color c, string box_txt) =>
    if timeframe.in_seconds("") <= timeframe.in_seconds(tf_limit) and use
        if t and not t[1]
            _c = get_box_color(c)
            _t = get_text_color(c)
            kz._box.unshift(box.new(time, high, time, low, xloc = xloc.bar_time, border_color = show_kz ? _c : na, bgcolor = show_kz ? _c : na, text = (show_kz and show_kz_text) ? box_txt : na, text_color = _t))

            if show_pivots
                kz._hi_line.unshift(line.new(time, high, time, high, xloc = xloc.bar_time, style = kzp_style, color = c, width = kzp_width))
                kz._lo_line.unshift(line.new(time, low, time, low, xloc = xloc.bar_time, style = kzp_style, color = c, width = kzp_width))
                if show_midpoints
                    kz._md_line.unshift(line.new(time, math.avg(high, low), time, math.avg(high, low), xloc = xloc.bar_time, style = kzm_style, color = c, width = kzm_width))
                    array.unshift(kz._md_valid, true)

                array.unshift(kz._hi_valid, true)
                array.unshift(kz._lo_valid, true)
            
            del_kz(kz)
        adjust_in_kz(kz, t)
        adjust_out_kz(kz, t)

if enable_sessionsandkillzones
    manage_kz(as_s, use_asian_session, t_as, as_color, as_txt)
    manage_kz(ls_s, use_london_session, t_ls, ls_color, ls_txt)
    manage_kz(nys_s, use_newyork_session, t_ns, nys_color, nys_txt)
    manage_kz(as_kz, use_asia_killzone, t_ak, ak_color, ak_txt)
    manage_kz(lo_kz, use_london_killzone, t_lo, lo_color, lo_txt)
    manage_kz(na_kz, use_nyam_killzone, t_na, na_color, na_txt)
    manage_kz(nl_kz, use_nylu, t_nl, nl_color, nl_txt)
    manage_kz(np_kz, use_nypm_killzone, t_np, np_color, np_txt)

if enable_dwm
    dwm()

if enable_extra_options
    hz_line(use_h1, t_h1, hz_h1, h1_text, h1_color)
    hz_line(use_h2, t_h2, hz_h2, h2_text, h2_color)

if enable_ma
    if use_ma1
        source1 = get_source(source_ma1)
        ma1 := get_ma(type_ma1, source1, length_ma1)
    if use_ma2
        source2 = get_source(source_ma2)
        ma2 := get_ma(type_ma2, source2, length_ma2)
    if use_ma3
        source3 = get_source(source_ma3)
        ma3 := get_ma(type_ma3, source3, length_ma3)
    if use_ma4
        source4 = get_source(source_ma4)
        ma4 := get_ma(type_ma4, source4, length_ma4)

plot(use_ma1 ? ma1 : na, title="MA 1", color=color_ma1, linewidth=1)
plot(use_ma2 ? ma2 : na, title="MA 2", color=color_ma2, linewidth=1)
plot(use_ma3 ? ma3 : na, title="MA 3", color=color_ma3, linewidth=1)
plot(use_ma4 ? ma4 : na, title="MA 4", color=color_ma4, linewidth=1)
